plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'

    id "com.gorylenko.gradle-git-properties" version "2.2.0"
    id 'edu.sc.seis.launch4j' version '2.4.6'
}

group 'jp.katana.gui'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url "https://hiroki19990625.github.io/maven-repo/"
    }
}

configurations {
    extraLibs
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation 'no.tornado:tornadofx:1.7.19'

    extraLibs files("libs/dockfx-0.4.jar")
    implementation 'org.controlsfx:controlsfx:8.40.15'
    implementation 'org.fxmisc.richtext:richtextfx:1.0.0-SNAPSHOT'

    implementation "jp.katana.server:katana-server:1.0-SNAPSHOT"
    implementation "jp.katana.core:katana-core:1.0-SNAPSHOT"
    implementation "jp.katana.math:katana-math:1.0-SNAPSHOT"
    implementation "jp.katana.utils:katana-utils:1.0-SNAPSHOT"

    testImplementation "org.junit.jupiter:junit-jupiter:5.5.1"

    configurations.implementation.extendsFrom(configurations.extraLibs)
}

// キャッシュを無効化
configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'jp.katana.gui.Main'
        attributes 'Multi-Release': 'true'
    }

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

launch4j {
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveFile.get().asFile.name}"
    icon = "${project.rootDir}/katana-gui.ico"
    cmdLine = "-Dterminal.jline=false"

    headerType = 'gui'
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

gitProperties {
    gitPropertiesName = "katana-gui-git.properties"
    gitPropertiesResourceDir = "${project.rootDir}/src/main/resources"
}